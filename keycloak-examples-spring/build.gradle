plugins {
  id 'org.springframework.boot' version '3.3.2' apply false
  id 'io.spring.dependency-management' version '1.1.0' apply false
}

subprojects {
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'

  group = 'keycloak-examples'
  version = '0.0.0-SNAPSHOT'
  sourceCompatibility = '21'
  targetCompatibility = '21'

  configurations {
    developmentOnly
    runtimeClasspath {
      extendsFrom developmentOnly
    }
    compileOnly {
      extendsFrom annotationProcessor
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // test
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
      exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // mockito
    testRuntimeOnly 'org.mockito:mockito-core'
  }

  test {
    useJUnitPlatform()
  }
  
  checkstyle {
    configFile = file("${rootProject.projectDir}/etc/checkstyle/checkstyle.xml")
    toolVersion = '8.33'
    ignoreFailures = false
  }

  jacocoTestReport {
    reports {
      html.required = true
      xml.required = false
      csv.required = false
    }
  }
}
